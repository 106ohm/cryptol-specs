module utils where

while : {a} (a -> Bit) -> (a -> a) -> a -> a
while condition body initial_state =
  if(condition initial_state) then while condition body (body initial_state)
  else initial_state

dowhile : {a} (a -> Bit) -> (a -> a) -> a -> a
dowhile condition body initial_state =
  if(condition next_state) then while condition body next_state else next_state
  where next_state = body initial_state

ZtoBV : {p, a} (fin p, p >= 1, fin a) => Z p -> [a]
ZtoBV x = (fromInteger (fromZ x))

BVtoZ : {p, a} (fin p, p >= 1, fin a) => [a] -> Z p
BVtoZ x = (fromInteger (toInteger x))

ZtoZ : {p, q} (fin p, fin q, p >= 1, q >= 1) => Z p -> Z q
ZtoZ x = (fromInteger (fromZ x))

isEven : Integer -> Bit
isEven x = ~((fromInteger x) : [1]) ! 0

mul2 x = x + x
mul3 x = x + mul2 x
mul4 x = mul2(mul2 x)
mul8 x = mul2(mul4 x)

type constraint isOdd a = (a / 2) * 2 == a - 1

half : {p} (fin p, p >= 3, isOdd p) => Z p -> Z p
half x = if isEven (fromZ x) then x/2 else (fromInteger ((fromZ x + `p) / 2))

mp_mod_inv : {a} (fin a, a >= 1) => Z a -> Z a
mp_mod_inv c = if c == 0 then error "Zero does not have a multiplicative inverse"
               else fromInteger (if u'' == 1 then x1'' else x2'')
  where
    innercond (a, x) = isEven a
    innerbody (a, x) = (a / 2, if isEven x then x / 2 else (x + `a) / 2)

    outtercond (u, v, x1, x2) = (u != 1) /\ (v != 1)
    outterbody (u, v, x1, x2) = if(u' >= v') then (u' - v', v', x1' - x2' % `a, x2')
                                             else (u', v' - u', x1', x2' - x1' % `a)
      where
        (u', x1') = while innercond innerbody (u, x1)
        (v', x2') = while innercond innerbody (v, x2)

    (u'', _, x1'', x2'') = while outtercond outterbody (fromZ c, `a, 1, 0)

